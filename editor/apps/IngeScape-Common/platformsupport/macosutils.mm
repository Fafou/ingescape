/*
 *	IngeScape Common
 *
 *  Copyright Â© 2019 Ingenuity i/o. All rights reserved.
 *
 *	See license terms for the rights and conditions
 *	defined by copyright holders.
 *
 *
 *	Contributors:
 *      Alexandre Lemort   <lemort@ingenuity.io>
 *
 */


#include "macosutils.h"

#include <QDebug>

#import <Foundation/Foundation.h>
#import <AppKit/AppKit.h>



//---------------------------------------------------------------------
//
//
//  OBJECTIVE-C
//
//
//---------------------------------------------------------------------



//
// Interface
//
@interface MacosUtilsNative : NSObject
// Activity used to prevent app nap
@property (atomic, strong) id preventAppNapActivity;


// Subscribe to system power notifications
-(void)subcribeToSystemPowerNotification;

// Enable App Nap
-(void)enableAppNap;

// Disable App Nap
-(void)disableAppNap;


@end



//
// Implementation
//
@implementation MacosUtilsNative

@synthesize preventAppNapActivity = _preventAppNapActivity;


// Callback NSWorkspaceWillSleepNotification
-(void)receiveSleepNotification: (NSNotification*) notification
{
#pragma unused(notification)
    MacosUtils::instance().systemSleep();
}


// Callback receiveWakeNotification
-(void)receiveWakeNotification: (NSNotification*) notification
{
#pragma unused(notification)
    MacosUtils::instance().systemWake();
}


// Subscribe to system power notifications
-(void)subcribeToSystemPowerNotification
{
    [[[NSWorkspace sharedWorkspace] notificationCenter] addObserver: self
                                                        selector: @selector(receiveSleepNotification:)
                                                        name: NSWorkspaceWillSleepNotification
                                                        object: nil];



    [[[NSWorkspace sharedWorkspace] notificationCenter] addObserver: self
                                                        selector: @selector(receiveWakeNotification:)
                                                        name: NSWorkspaceDidWakeNotification
                                                        object: nil];
}


// Enable App Nap
-(void)enableAppNap
{
   if (_preventAppNapActivity != nil)
   {
       NSProcessInfo* processInfo = [NSProcessInfo processInfo];
       if (
           (processInfo != nil)
           &&
           [processInfo respondsToSelector:@selector(beginActivityWithOptions:reason:)]
           )
       {
           _preventAppNapActivity = [processInfo beginActivityWithOptions:NSActivityUserInitiatedAllowingIdleSystemSleep
                                                 reason:@"Disable App Nap from Qt"];
       }
   }
}


// Disbale App Nap
-(void)disableAppNap
{
    if (_preventAppNapActivity != nil) {

        [[NSProcessInfo processInfo] endActivity:_preventAppNapActivity];
        _preventAppNapActivity = nil;
    }
}

@end




//---------------------------------------------------------------------
//
//
//  PRIVATE API
//
//
//---------------------------------------------------------------------


/**
 * @brief The MacosUtilsPrivate class defines a private API used to store Objective-C objects
 */
class MacosUtilsPrivate {
public:
    /**
     * @brief Constructor
     */
    MacosUtilsPrivate()
        : objectiveCAPI(nil)
    {
    }


    MacosUtilsNative* objectiveCAPI;
};




//---------------------------------------------------------------------
//
//
//  MacosUtils
//
//
//---------------------------------------------------------------------



/**
 * @brief Constructor
 */
MacosUtils::MacosUtils(QObject* parent)
    : QObject(parent),
      _privateAPI(new MacosUtilsPrivate())
{
}


/**
 * @brief Destructor
 */
MacosUtils::~MacosUtils()
{
}


/**
 * @brief Get our singleton instance
 * @return
 */
MacosUtils& MacosUtils::instance()
{
    static MacosUtils instance;
    return instance;
}


/**
 * @brief Init
 */
void MacosUtils::init()
{
    if (_privateAPI->objectiveCAPI == nil)
    {qDebug() << "NILL";
        MacosUtilsNative* macosUtilsNative = [[MacosUtilsNative alloc] init];
        _privateAPI->objectiveCAPI = macosUtilsNative;
    }

    _subscribeToSystemPowerNotifications();
}


/**
 * @brief Clean
 */
void MacosUtils::clean()
{
}


/**
 * @brief Remove all menu items generated by Qt to handle conventions
 */
void MacosUtils::removeOSGeneratedMenuItems()
{
    // Clean-up our Edit menu
    _removeMenuItemStartDictation();
    _removeMenuItemEmojiAndSymbols();
}


/**
 * @brief Enable or disable energy efficiency features
 *
 * @param value
 */
void MacosUtils::energyEfficiencyFeaturesEnabled(bool value)
{
    if (value)
    {
        [_privateAPI->objectiveCAPI enableAppNap];
    }
    else
    {
        [_privateAPI->objectiveCAPI disableAppNap];
    }
}



/**
 * @brief Subscribe to system power notifications
 */
void MacosUtils::_subscribeToSystemPowerNotifications()
{
    [_privateAPI->objectiveCAPI subcribeToSystemPowerNotification];
}




/**
 * @brief Remove the "Start Dictation..." menu item from the "Edit" menu
 */
void MacosUtils::_removeMenuItemStartDictation()
{
    [[NSUserDefaults standardUserDefaults] setBool:YES forKey:@"NSDisabledDictationMenuItem"];
}


/**
 * @brief Remove the "Emoji & Symbols" menu item from the "Edit" menu
 */
void MacosUtils::_removeMenuItemEmojiAndSymbols()
{
    [[NSUserDefaults standardUserDefaults] setBool:YES forKey:@"NSDisabledCharacterPaletteMenuItem"];
}

